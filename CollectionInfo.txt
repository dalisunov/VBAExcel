VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "CollectionInfo"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' ------------------------------------------------------------------
' # Объявление типа данных
' ------------------------------------------------------------------

Private Type TCollectionInfo
    Name As String
    ItemCount As Long
    CollectionType As String ' "Collection" или "Dictionary"
    keys() As Variant
    Items() As Variant
    ItemTypes() As String
End Type

' ------------------------------------------------------------------
' # Переменные класса
' ------------------------------------------------------------------

Private this As TCollectionInfo

' ------------------------------------------------------------------
' # Объявление событий
' ------------------------------------------------------------------

Public Event OnInfoChanged(ByVal propertyName As String, ByVal oldValue As Variant, ByVal newValue As Variant)
Public Event OnKeysChanged()
Public Event OnItemsChanged()
Public Event OnTypesChanged()

' ------------------------------------------------------------------
' # Методы работы со свойствами коллекции
' ------------------------------------------------------------------

' # [Property Let name] - Установка имени коллекции
Public Property Let Name(ByVal Value As String)
    Dim oldValue As String
    oldValue = this.Name
    this.Name = Value
    RaiseEvent OnInfoChanged("Name", oldValue, Value)
End Property

' # [Property Get name] - Получение имени коллекции
Public Property Get Name() As String
    Name = this.Name
End Property

' # [Property Get ItemCount] - Получение количества элементов
Public Property Get ItemCount() As Long
    ItemCount = this.ItemCount
End Property

' # [Property Let ItemCount] - Установка количества элементов
Public Property Let ItemCount(ByVal Value As Long)
    this.ItemCount = Value
End Property

' # [Property Get CollectionType] - Получение типа коллекции
Public Property Get CollectionType() As String
    CollectionType = this.CollectionType
End Property

' # [Property Let CollectionType] - Установка типа коллекции
Public Property Let CollectionType(ByVal Value As String)
    this.CollectionType = Value
End Property


' ------------------------------------------------------------------
' # Методы работы с ключами коллекции
' ------------------------------------------------------------------

' # [Sub SetKeys] - Установка ключей коллекции
Public Sub SetKeys(ByRef keys() As Variant)
    this.keys = keys
    RaiseEvent OnKeysChanged
    RaiseEvent OnInfoChanged("Keys", Empty, Empty) 'Сигнализируем об изменении, но старое/новое значение не передаем.
End Sub

' # [Function GetKeys] - Получение ключей коллекции
Public Function GetKeys() As Variant()
    GetKeys = this.keys
End Function


' ------------------------------------------------------------------
' # Методы работы с элементами коллекции
' ------------------------------------------------------------------

' # [Sub SetItems] - Установка элементов коллекции
Public Sub SetItems(ByRef Items() As Variant)
    this.Items = Items
    RaiseEvent OnItemsChanged
    RaiseEvent OnInfoChanged("Items", Empty, Empty) 'Сигнализируем об изменении, но старое/новое значение не передаем.
End Sub

' # [Function GetItems] - Получение элементов коллекции
Public Function GetItems() As Variant()
    GetItems = this.Items
End Function


' ------------------------------------------------------------------
' # Методы работы с типами элементов коллекции
' ------------------------------------------------------------------

' # [Sub SetItemTypes] - Установка типов элементов коллекции
Public Sub SetItemTypes(ByRef Types() As String)
    this.ItemTypes = Types
    RaiseEvent OnTypesChanged
    RaiseEvent OnInfoChanged("ItemTypes", Empty, Empty) 'Сигнализируем об изменении, но старое/новое значение не передаем.
End Sub

' # [Function GetItemTypes] - Получение типов элементов коллекции
Public Function GetItemTypes() As String()
    GetItemTypes = this.ItemTypes
End Function
